s              = file / code
code           = (expr <end> | statement <end>)*

file           = import-block? (method | var-declare <';'> | class | _typedef)*
method         = <'static'?> 
                 <dtype>? identifier
                 lambda-args (lambda-body | <'=>'> assign-val | <';'>) <';'*>

import-block   = (import <';'?>)+ 
<import>       = (import-full | import-as | import-hide | import-show | import-naked)
import-naked   = im-body
import-full    = im-body im-as-part (im-show-part | im-hide-part)  
import-as      = im-body im-as-part
import-show    = im-body im-show-part
import-hide    = im-body im-hide-part
<im-body>      = <'import'> string
<im-as-part>   = (<'deferred'?> <'as'> identifier)
<im-show-part> = <'show'> identifier+
<im-hide-part> = <'hide'> identifier+

class          = <'abstract'? 'class' #'[^{}]*' '{'>
                    (method | getter-decl | 
                     var-declare | <construct-decl> )*
                 <'}'>
construct-decl = 'const'? identifier ('.' identifier)?  
                 '(' ( 
                       (typed-arg '.')* typed-arg? 
                       (<','> (typed-arg '.')* typed-arg)*
                     )?
                     <','>? args-bloc? 
                  ')'
                 ( (':' 'super' params)? ';' | ':' (identifier '=' expr (','|';'))* )
args-bloc      = '{' #'[^{}]*' '}' 
_typedef       = 'typedef' dtype <'='> (dtype | method) 
getter-decl    = <dtype?> 'get'
                 identifier (lambda-body | <'=>'> assign-val | <';'>) <';'*>

<end>          = <(';'|#'\n'+|','|#'$')>
<statement>    = if | var-declare
return         = <'return'> expr? <';'>

if             = <'if'> if-condition if-else-body
<if-else-body> =  (lambda-body | return | expr <end>) 
                  ( (<'else'> | <'else if'> if-condition) if-else-body)*   
<if-condition> = <'('> expr <')'> 

var-declare    = <'static'>? field-type var-init (<','> var-init)* <';'>*
var-init       = identifier (<'='> expr)?

lambda-args    = <'('> typed-arg? (<','>  typed-arg)* <')'> <'async'?>  
<typed-arg>    = <dtype?> identifier  
lambda-body    = <'{'> ( statement <';'> | expr <end> | method )* return? <'}'>

expr-list      = expr (',' expr)*
<expr>         = lambda-val <'!'>?

<lambda-val>   = cascade-val | lambda 
lambda         = lambda-args lambda-body | lambda-args <'=>'> assign-val
<cascade-val>  = assign-val | cascade
cascade        = cascade-val <'..'> assign-val
<assign-val>   = ternary-val | _set
_set           = dot-val <_operator> ternary-val 
<ternary-val>  = ifnull-val | ternary
ternary        = ternary-val <'?'> expr <':'> expr
<ifnull-val>   = and-val | ifnull
ifnull         = ifnull-val '??' and-val
<and-val>      = or-val | and
and            = and-val <'&&'> or-val
<or-val>       = equality-val | or
or             = or-val <'||'> equality-val
<equality-val> = compare-val | equality
equality       = equality-val ( '==' | '!=' ) compare-val
<compare-val>  = add-sub-val | compare 
compare        = compare-val ( '<' | '>' | '<=' | '>=' | 'is') add-sub-val 
<add-sub-val>  = mul-div-val | add | sub
add            = add-sub-val <'+'> mul-div-val
sub            = add-sub-val <'-'> mul-div-val
<mul-div-val>  = dot-val | mul | div
mul            = mul-div-val <'*'> dot-val
div            = mul-div-val ('/' | '%' | '~/') dot-val
<dot-val>      = term | dot | dot-op
dot-op         = dot-val <'?'> (invoke | field)
dot            = dot-val (invoke | field)
<term>         = value | <'('> ternary-val <')'>

<value>        = (number | string | !get identifier
               | constructor | invoke | typecasting | const  
               | list | map | get | _spread
               | inc | _inc | dec | _dec | identifier | number
               | _tilda | neg | not | await) <';'?> 

typecasting    = value 'as' dtype

invoke         = <'.'> typed-value params  <';'?>
field          = <'.'> identifier <';'?>
await          = <'await'> (value)
neg            = !number <'-'> value
not            = <'!'> value
dec            = <'--'> value
inc            = <'++'> value
_dec           = value <'--'>
_inc           = value <'++'>
_tilda         = <'~'> value

const          = <'const'> (constructor | get | map | list | dot | dot-op)
constructor    = typed-value params <';'?>
<typed-value>  = identifier <type-args?> 
params         = <'('>  <')'>
               | (<'('> argument (<','> argument)* <','?> <')'>)
argument       = expr | (named-arg expr) 
named-arg      = identifier <':'> 

list           = <type-args?> 
                 <'['> (list-el (<','> list-el)* <','?> | EPSILON ) <']'>
<list-el>      = _spread | expr     
_spread        = '...' value 
map            = <type-args?> 
                 <'{'> (expr <':'> expr
                        (<','> (identifier|string|number) <':'> expr)* 
                        | EPSILON ) <'}'>
get            = (get | identifier | map | dot | dot-op)
                 <'['> (identifier | string | number | dot | dot-op) <']'>     

_operator      = '=' | '*=' | '/=' | '~/=' | '%=' | '+=' | '<<=' | '>>=' 
               | '>>>=' | '&=' | '^=' | '|=' | '??=' | '-='

(* type without name *)
<field-type>   = !'await' (modifier <dtype> | !modifier <dtype> | modifier)
<modifier>     = 'final' | 'const'
dtype          = qualified type-args? <'?'?> 
qualified      = (identifier '.')* identifier
type-args      = '<' type-list '>'
type-list      = dtype (',' dtype)*

string         = #'r?\'([^\']*)\''
identifier     = !return #'[a-zA-Z_]+[a-zA-Z0-9!_]*' 
number         = #'-?[0-9]*\.?[0-9]+((e|E)(\+|\-)[0-9]+)?'

<digit>        = #'[0-9]'
