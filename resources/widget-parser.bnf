s              = (statement | expr) ((<';'>|<#'\n'+>) s)* <','?>

<value>        = (number | string | !get identifier
               | invocation | !invocation constructor 
               | list | map | get | typed-value | lambda
               | inc | _inc | dec | _dec | identifier | number
               | _tilda | neg | not | await) <';'?> 

typed-value    = value 'as' identifier type?

<statement>    = if | return
return         = <'return'> expr;

invocation     = (list | map | constructor | priority) 
                 ((<'.'>|<'..'>) (constructor|identifier))+
constructor    = 'const'? identifier <type?> params <';'?>
params         = <'('>  <')'>
               | (<'('> argument (<','> argument)* <','?> <')'>)
argument       = expr | (named-arg expr) 
named-arg      = identifier <':'> 

list           = 'const'? <type?> 
                 <'['> list-el (<','> list-el)* <','?> <']'>
<list-el>      = spread | value      
spread         = '...' value 
map            = 'const'? <type?> 
                 <'{'> (identifier|string|number) <':'> value
                 (<','> (identifier|string|number) <':'> value)*  <'}'>
get            = (get | identifier) <'['> (identifier | string | number) <']'>     

if             = <'if'> if-condition if-else-body
<if-else-body> =  (lambda-body | s ) 
                  ( (<'else'> | <'else if'> if-condition) if-else-body)*   
<if-condition> = <'('> expr <')'> 

lambda         = lambda-args <'async'?> (lambda-body | <'=>'> s)
lambda-args    = <'('> typed-arg? (<','> typed-arg)* <')'> 
<typed-arg>    = <(identifier <type?>)?> identifier  
lambda-body    = <'{'> s <'}'>

<expr>         = assign-val 
<assign-val>   = ternary-val | _assign
_assign        = ('const'|'final')? (identifier type?)? identifier 
                 <_operator> ternary-val 
<ternary-val>  = ifnull-val | ternary
ternary        = ternary-val <'?'> ifnull-val <':'> ifnull-val
<ifnull-val>   = and-val | ifnull
ifnull         = ifnull-val '??' and-val
<and-val>      = or-val | and
and            = and-val '&&' or-val
<or-val>       = equality-val | or
or             = or-val '||' equality-val
<equality-val> = compare-val | equality
equality       = equality-val ( '==' | '!=' ) compare-val
<compare-val>  = add-val | compare 
compare        = compare-val ( '<' | '>' | '<=' | '>=' | 'is' | 'as') add-val 
<add-val>      = mul-val | add
add            = add-val ('+' | '-') mul-val
<mul-val>      = term | mul
mul            = mul-val ('*' | '/' | '%' | '~/') term
<term>         = value | <'('> ternary-val <')'>

await          = <'await'> value
neg            = !number <'-'> value
not            = <'!'> value
dec            = <'--'> value
inc            = <'++'> value
_dec           = value <'--'>
_inc           = value <'++'>
_tilda         = <'~'> value

_assignment    = identifier _operator value
_operator      = '=' | '*=' | '/=' | '~/=' | '%=' | '+=' | '<<=' | '>>=' 
               | '>>>=' | '&=' | '^=' | '|=' | '??='
priority       = <'('> value <')'> 

<type>         = '<' (identifier type?) (',' (identifier type?))* '>'
string         = #"'([^']*)'"
<identifier>   = #'[a-zA-Z_]+[a-zA-Z0-9.?!_]*' 
number         = #'-?[0-9]*\.?[0-9]+((e|E)(\+|\-)[0-9]+)?'

<digit>        = #'[0-9]'
