s              = statement | expr

<value>        = (number | string | !get identifier
               | invocation | !invocation constructor 
               | list | map | get | convertion
               | inc | _inc | dec | _dec | identifier | number
               | _tilda | neg | not ) <';'?> 

<statement>    = _assignment | if | return
return         = <'return'> expr;

invocation     = (constructor | priority) (<'.'> (constructor|identifier))*
constructor    = <'return'?> 'const'? identifier params <';'?>
params         = <'('>  <')'>
               | (<'('> argument (<','> argument)* <','?> <')'>)
argument       = (expr | lambda) | (named-arg (expr | lambda)) 
named-arg      = identifier <':'> 

list           = 'const'? <type?> 
                 <'['> list-el (<','> list-el)* <','?> <']'>
<list-el>      = spread | value      
spread         = '...' value 
map            = 'const'? <type?> 
                 <'{'> (identifier|string|number) <':'> value
                 (<','> (identifier|string|number) <':'> value)*  <'}'>
get            = (get | identifier) <'['> (identifier | string | number) <']'>     

if             = <'if'> if-condition if-else-body
<if-else-body> =  (lambda-body | s <';'>) 
                  ( (<'else'> | <'else if'> if-condition) if-else-body)*   
<if-condition> = <'('> expr <')'> 

lambda         = lambda-args <'async'?> (lambda-body | <'=>'> s)
lambda-args    = <'('> typed-arg? (<','> typed-arg)* <')'> 
<typed-arg>    = <(identifier <type?>)?> identifier  
lambda-body    = <'{'> s (<';'> s)* <'}'>

<expr>         = ternary-val
<ternary-val>  = ifnull-val | ternary
ternary        = ternary-val <'?'> ifnull-val <':'> ifnull-val
<ifnull-val>   = and-val | ifnull
ifnull         = ifnull-val '??' and-val
<and-val>      = or-val | and
and            = and-val '&&' or-val
<or-val>       = equality-val | or
or             = or-val '||' equality-val
<equality-val> = compare-val | equality
equality       = equality-val ( '==' | '!=' ) compare-val
<compare-val>  = add-val | compare 
compare        = compare-val ( '<' | '>' | '<=' | '>=' ) add-val 
<add-val>      = mul-val | add
add            = add-val ('+' | '-') mul-val
<mul-val>      = term | mul
mul            = mul-val ('*' | '/' | '%' | '~/' ) term
<term>         = value | <'('> add-val <')'>

neg            = !number <'-'> value
not            = <'!'> value
dec            = <'--'> value
inc            = <'++'> value
_dec           = value <'--'>
_inc           = value <'++'>
_tilda         = <'~'> value

_assignment    = identifier _operator value
_operator      = '=' | '*=' | '/=' | '~/=' | '%=' | '+=' | '<<=' | '>>=' 
               | '>>>=' | '&=' | '^=' | '|=' | '??='
convertion     = value 'as' identifier type? 
priority       = <'('> value <')'> 

<type>         = '<' (identifier type?) (',' (identifier type?))* '>'
string         = #'([\"\'])(?:(?=(\\?))\2.)*?\1'
<identifier>   = #'[a-zA-Z_]+[a-zA-Z0-9.?_]*' 
number         = #'-?[0-9]*\.?[0-9]+((e|E)(\+|\-)[0-9]+)?'

<digit>        = #'[0-9]'
nobracket      = #'[^{}]'
